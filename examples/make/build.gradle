plugins {
    id 'com.cisco.external-build' version '1.10'
}

import com.cisco.gradle.externalbuild.ExternalNativeExecutableSpec
import com.cisco.gradle.externalbuild.tasks.GnuMake

def getIntermediateDir = { binary ->
    file("${buildDir}/external-build/${binary.component.name}/${binary.targetPlatform.name}")
}

def getInstallDir = { binary ->
    file("${buildDir}/install/${binary.component.name}/${binary.targetPlatform.name}")
}

model {
    components {
        hello(ExternalNativeExecutableSpec) {
            buildConfig(GnuMake) {
                def intDir = getIntermediateDir(binary)
                def outputDir = getInstallDir(binary)

                // Uncomment for easier debugging
                //redirectOutput = false

                makefile 'Makefile'
                targets 'all', 'install'

                inputs.dir 'src'
                inputs.file 'Makefile'
                outputs.dir outputDir

                workingDir intDir
                environment = [
                    'PATH': toolChainPath,
                    'OUTPUT_DIR': outputDir,
                ]
            }

            buildOutput {
                def outputDir = getInstallDir(binary)
                outputFile = file("${outputDir}/hello")
            }
        }
    }
}

